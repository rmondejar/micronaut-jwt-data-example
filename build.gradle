plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id "groovy"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "com.google.cloud.tools.jib" version "2.1.0"
}

version "0.3"
group "mn.jwt.data"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

configurations {
    developmentOnly 
}

ext {
    googleProjectId= "my-project"
    micronautVersion = "2.0.0.M2"
    micronautDataVersion = "1.0.2"
    lombokVersion = "1.18.12"
}

dependencies {

    //tag::lombok
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    //end::lombok

    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor "io.micronaut.configuration:micronaut-openapi"
    annotationProcessor "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    annotationProcessor enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")

    implementation "io.micronaut:micronaut-security-jwt"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "javax.annotation:javax.annotation-api"
    implementation "io.swagger.core.v3:swagger-annotations"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

    //tag::data
    annotationProcessor "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    implementation "io.micronaut.data:micronaut-data-hibernate-jpa:$micronautDataVersion"
    implementation "io.micronaut.configuration:micronaut-hibernate-jpa:$micronautVersion"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "io.micronaut.configuration:micronaut-jdbc-hikari:$micronautVersion"
    //runtime 'org.postgresql:postgresql:42.2.4'
    //end::data

    //tag::test
    testAnnotationProcessor enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    //end::test
}

test.classpath += configurations.developmentOnly

mainClassName = "mn.jwt.data.Application"

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
}
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

jib {
    to {
        image = "gcr.io/$googleProjectId/mn-jwt-data"
    }
    from {
        image = "adoptopenjdk/openjdk11-openj9:jdk-11.0.1.13-alpine-slim"
    }
    container {
        jvmFlags = [
                // this flag is appropriate for Servlet deployments
                // reducing startup time by constraining the JIT
                '-XX:TieredStopAtLevel=1',
                '-XX:MaxRAM=256m'
        ]
    }
}